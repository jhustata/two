{
 "cells": [
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Code block followed with documentation!\n",
    "\n",
    "```stata\n",
    "capture log close\n",
    "log using lecture4.log, replace text\n",
    "\n",
    "//This log file contains most of the examples used in Lecture 4 of\n",
    "//summer Stata Programming and Data Management, \n",
    "///along with additional explanations and examples.\n",
    "\n",
    "version 12 //We're teaching new-style merging\n",
    "clear all //clear all data from memory\n",
    "macro drop _all //clear all macros in memory\n",
    "set more off   //give output all at once (not one screenful at a time)\n",
    "set linesize 80 //maximum allowed width for output\n",
    "\n",
    "```\n",
    "\n",
    "\n",
    "In the code above, the capture log close command is used to close any previously open log files. The log using lecture4.log, replace text command opens a new log file named \"lecture4.log\" and replaces its contents if it already exists. The text option specifies that both the executed code and the output should be logged.\n",
    "\n",
    "The subsequent comments provide a brief description of the log file's purpose, stating that it contains examples used in Lecture 4 of a Stata Programming and Data Management course, along with additional explanations and examples.\n",
    "\n",
    "The version 12 command sets the Stata version to 12, indicating that the code is compatible with that version. The clear all command clears all data from memory, removing any previously loaded datasets. The macro drop _all command clears all macros in memory. The set more off command disables the feature that displays output one screenful at a time, showing all output at once. Finally, the set linesize 80 command sets the maximum allowed width for output to 80 characters, ensuring that output is displayed within the specified limit.\n",
    "\n",
    "These initial steps help prepare the environment for executing the subsequent code and logging the results in the \"lecture4.log\" file."
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "```stata\n",
    "use transplants, clear\n",
    "\n",
    "//examples of syntax for merging\n",
    "//NOTE: after each example we reload transplants.dta\n",
    "\n",
    "//simplest merge: link data in memory (transplants.dta) to donors_recipients.dta\n",
    "//by matching on the variable fake_id which appears in both datasets\n",
    "merge 1:1 fake_id using donors_recipients\n",
    "\n",
    "//the merge command creates a new variable, _merge\n",
    "//check how many records matched\n",
    "tab _merge\n",
    "\n",
    "//\"master\" = dataset that was in memory before we merged\n",
    "//\"using\" = dataset that we are bringing in with the merge command\n",
    "//\"match\" = appears in both master and using datasets\n",
    "\n",
    "```\n",
    "\n",
    "In the code above, we start by using the dataset transplants and clearing any existing data in memory. Then, we demonstrate examples of syntax for merging datasets.\n",
    "\n",
    "The first example shows the simplest merge where we link the data in memory (transplants.dta) to donors_recipients.dta using a one-to-one match on the variable fake_id which appears in both datasets. The merge command is used for merging.\n",
    "\n",
    "After the merge, a new variable called _merge is created. To check how many records matched during the merge, we use the tab command on _merge.\n",
    "\n",
    "In the context of merging, the terms \"master\" and \"using\" refer to the datasets involved in the merge operation. \"Master\" refers to the dataset that was in memory before the merge, while \"using\" refers to the dataset that we are bringing in with the merge command. The term \"match\" indicates the records that appear in both the master and using datasets.\n",
    "\n",
    "Please let me know if you need further assistance or if there's anything else I can help you with!"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "```stata\n",
    "use transplants, clear\n",
    "merge 1:1 fake_id using donors_recipients, keep(match)\n",
    "//only keep records that appear both in the original dataset and in the new dataset\n",
    "\n",
    "use transplants, clear\n",
    "merge 1:1 fake_id using donors_recipients, keep(master match)\n",
    "//only keep records that appear in the master dataset only, or both datasets\n",
    "\n",
    "use transplants, clear\n",
    "merge 1:1 fake_id using donors_recipients, keep(master using)\n",
    "//only keep records that appear in the master dataset only, or the using dataset only, but not both\n",
    "\n",
    "use transplants, clear\n",
    "merge 1:1 fake_id using donors_recipients, keep(master match) gen(mergevar)\n",
    "//call the newly created variable \"mergevar\" instead of \"_merge\"\n",
    "\n",
    "use transplants, clear\n",
    "merge 1:1 fake_id using donors_recipients, keep(match) nogen\n",
    "//don't create a new variable\n",
    "\n",
    "merge m:1 fake_don_id using donors, keep(match) nogen keepusing(age_don)\n",
    "//don't load all the new variables from the donors dataset, just load age_don.\n",
    "\n",
    "corr age*\n",
    "//are donor age and recipient age correlated in our fake data? No.\n",
    "\n",
    "//merging: using assert to make sure that all your records match\n",
    "use transplants, clear\n",
    "merge 1:1 fake_id using donors_recipients, keep(master match)\n",
    "assert _merge==3\n",
    "drop _merge\n",
    "\n",
    "//merging: using assert to make sure that *most* of your records match\n",
    "use transplants, clear\n",
    "merge 1:1 fake_id using donors_recipients, keep(master match)\n",
    "assert inlist(_merge, 1, 3) //master only or matched\n",
    "quietly count if _merge == 3\n",
    "assert r(N)/_N > 0.99  //99% of records have _merge==3\n",
    "drop _merge\n",
    "\n",
    "\n",
    "```\n",
    "\n",
    "In the code above, we continue with examples of merging datasets using different options:\n",
    "\n",
    "The first example merges the datasets transplants.dta and donors_recipients.dta based on a one-to-one match on the variable fake_id. It keeps only the records that appear in both datasets using the keep(match) option.    \n",
    "   \n",
    "The second example keeps records that appear in the master dataset only or in both datasets using the keep(master match) option.\n",
    "The third example keeps records that appear in the master dataset only or the using dataset only, but not in both, using the keep(master using) option.     \n",
    "     \n",
    "The fourth example creates a new variable called \"mergevar\" instead of the default _merge using the gen(mergevar) option.      \n",
    "The fifth example performs the merge but does not create a new variable using the nogen option.      \n",
    "The sixth example demonstrates merging using a one-to-many match (m:1) and selects specific variables to load from the donors dataset using the keepusing() option.     \n",
    "The code then computes the correlation between the variables age_don and age_rec using the corr command to check if donor age and recipient age are correlated in the fake data.      \n",
    "The next set of examples introduces the use of assert to check the validity of the merge. The first example uses assert to ensure that all records have a _merge value of 3 (indicating a match) and then drops the _merge variable.     \n",
    "The second example uses assert to check that most of the records have a _merge value of 1 (indicating they are in the master dataset) or 3 (indicating a match), with at least 99% of the records meeting this condition. The _merge variable is dropped afterwards.    "
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "```stata\n",
    "//by\n",
    "\n",
    "use transplants, clear\n",
    "sort abo\n",
    "by abo: gen cat_n = _N //cat_n = # records in each abo category\n",
    "\n",
    "sort abo age\n",
    "by abo: gen cat_id = _n //1 for youngest patient, 2 for next youngest, etc.\n",
    "\n",
    "\n",
    "//egen\n",
    "\n",
    "//obtaining mean age and storing it in a variable\n",
    "use transplants, clear\n",
    "egen mean_age = mean(age)\n",
    "\n",
    "//more egen examples\n",
    "egen median_age = median(age)\n",
    "egen max_age = max(age)\n",
    "egen min_age = min(age)\n",
    "egen age_q1 = pctile(age), p(25) //25th percentile\n",
    "egen age_sd = sd(age)  //standard deviation\n",
    "egen total_prev = sum(prev) //add all the values (all are 1 or 0 in this case)\n",
    "\n",
    "//using egen with by\n",
    "use transplants, clear\n",
    "sort dx\n",
    "by dx: egen mean_age = mean(age)\n",
    "by dx: egen min_bmi = min(bmi)\n",
    "\n",
    "//combining sort and by to get bysort / bys\n",
    "use transplants, clear\n",
    "bys abo: egen m_bmi=mean(bmi)\n",
    "bys abo gender: egen max_bmi = max(bmi)\n",
    "bys abo gender: egen min_bmi = min(bmi)\n",
    "gen spread = max_bmi - min_bmi\n",
    "\n",
    "//egen = tag\n",
    "egen grouptag = tag(abo gender)\n",
    "//now, for all groups of abo/gender, (1/M, 3/F, etc), there's exactly one\n",
    "//record that has grouptag = \"1\". All others have grouptag = \"0\".\n",
    "\n",
    "list abo gend spread if grouptag\n",
    "\n",
    "//more examples of bys\n",
    "use donors_recipients, clear\n",
    "bys fake_don: gen n_tx = _N\n",
    "bys fake_don: gen tx_id = _n\n",
    "egen don_tag = tag(fake_don)\n",
    "tab n_tx if don_tag\n",
    "\n",
    "\n",
    "//graphing example with egen and tag\n",
    "use transplants, clear\n",
    "egen tag_race = tag(race)\n",
    "bys race: egen mean_bmi=mean(bmi)\n",
    "bys race: egen mean_age=mean(age)\n",
    "\n",
    "label define race_label 1 \"White\" ///\n",
    "    2 \"Black/AA\" 4 \"Hispanic/Latino\" ///\n",
    "    5 \"East Asian\" 6 \"Native American\" ///\n",
    "    7 \"Asian Indian\" 9 \"Other\"  //create a label race_label\n",
    "\n",
    "//I personally find this more legible than cond() with seven cases,\n",
    "//although cond() has its champions\n",
    "gen racedesc = \"White/Caucasian\" if race==1\n",
    "replace racedesc = \"Black/AA\" if race==2\n",
    "replace racedesc = \"Hispanic/Latino\" if race==4\n",
    "replace racedesc = \"Native American\" if race==5\n",
    "replace racedesc = \"East Asian\" if race==6\n",
    "replace racedesc = \"Asian Indian\" if race==7\n",
    "replace racedesc = \"Other\" if race==9\n",
    "\n",
    "twoway scatter m~_bmi m~_age if tag_race, ///\n",
    "    title(\"Mean age and BMI by race/ethnicity\") ///\n",
    "    xtitle(\"Mean age\") ytitle(\"Mean BMI\") ///\n",
    "    xlabel(40(2)60) ylabel(20(2)30) ///\n",
    "    mlabel(racedesc)\n",
    "graph export graph_lecture4.png, replace width(2400)\n",
    "\n",
    "\n",
    "```\n",
    "\n",
    "In the code above, we cover examples related to the by"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "```stata\n",
    "//math functions\n",
    "\n",
    "//setup for math functions\n",
    "clear\n",
    "set obs 1000\n",
    "gen n = _n/100 - 5\n",
    "\n",
    "//rounding method 1: floor\n",
    "disp floor(0.3)\n",
    "disp floor(4.5)\n",
    "disp floor(8.9)\n",
    "\n",
    "//rounding method 2: ceil\n",
    "disp ceil(0.3)\n",
    "disp ceil(4.5)\n",
    "disp ceil(8.9)\n",
    "\n",
    "//rounding method 3: round\n",
    "disp round(0.3)\n",
    "disp round(4.5)\n",
    "disp round(8.9)\n",
    "\n",
    "//using round to round to something other than nearest integer\n",
    "disp round(-0.32, 0.1)  //round to nearest 0.1\n",
    "disp round(4.5, 2)   //round to nearest 2\n",
    "disp round(8.9, 10)  //round to nearest 10\n",
    "\n",
    "//min and max\n",
    "disp min(8,6,7,5,3,0,9) //display minimum value\n",
    "disp max(8,6,7,5,3,0,9) //display maximum value\n",
    "\n",
    "//other math functions\n",
    "disp exp(1)  //exponent. e^1 = e.\n",
    "disp ln(20)  //log of 20 ~ 3\n",
    "disp sqrt(729)  //27\n",
    "\n",
    "disp abs(-6)  //absolute value\n",
    "disp mod(529, 10) //modulus (remainder)\n",
    "disp c(pi)  //constant pi (for illustration of sine)\n",
    "disp sin(c(pi)/2) //sine function\n",
    "\n",
    "//string functions\n",
    "use transplants, clear\n",
    "list extended_dgn in 1/5, clean\n",
    "\n",
    "disp word(\"Hello, is there anybody in there?\", 4)\n",
    "list extended_dgn if word(ext, 5) != \"\", clean noobs\n",
    "disp strlen(\"Same as it ever was\")\n",
    "list extended_dgn if strlen(ext)< 6, clean\n",
    "assert regexm(\"Earth\", \"art\")\n",
    "assert !regexm(\"team\", \"I\")\n",
    "\n",
    "tab ext if regexm(ext, \"HTN\")\n",
    "list ext if regexm(ext, \"^A\") \n",
    "//starts with A\n",
    "\n",
    "list ext if regexm(ext, \"X$\") \n",
    "//ends with X\n",
    "\n",
    "tab ext if regexm(ext, \"HIV.*Y\") \n",
    "//contains \"HIV\", then some other stuff, then Y\n",
    "```\n",
    "\n",
    "That's it with the math functions"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "\n",
    "```stata\n",
    "//date and time functions\n",
    "//review of number formats\n",
    "disp %3.2f exp(1)\n",
    "disp %4.1f 3.14159\n",
    "\n",
    "//examples of the %td format for dates (days since 1/1/1960)\n",
    "disp %td 19400\n",
    "disp %td 366\n",
    "disp %td -5\n",
    "\n",
    "//doing math on Stata dates\n",
    "use transplants, clear\n",
    "gen oneweek = transplant_date +7\n",
    "format %td oneweek\n",
    "list transplant_date  oneweek in 1/3\n",
    "\n",
    "//date functions\n",
    "//td() function to give the integer date for a given calendar date\n",
    "disp td(04jul1976)\n",
    "\n",
    "//\"Back to the future\" start date\n",
    "disp td(26oct1985) \n",
    "\n",
    "//mdy() function to give the integer date for a given month/day/year\n",
    "disp mdy(7,4,1976)\n",
    "\n",
    "//\"Back to the future\" start date\n",
    "disp mdy(10,26,1985) \n",
    "\n",
    "\n",
    "\n",
    "//date() function to convert strings to dates\n",
    "//Woodstock festival starts\n",
    "disp date(\"August 15, 1969\", \"MDY\")\n",
    "//Next perihelion of Halley's comet\n",
    "disp date(\"2061 28 July\", \"YDM\")\n",
    "\n",
    "use donors, clear\n",
    "gen donor_dob = date(fake_don_dob, \"DMY\")\n",
    "\n",
    "format %td donor_dob\n",
    "list donor_dob fake_don_dob in 1/5\n",
    "\n",
    "use transplants, clear\n",
    "//survival analysis overview\n",
    "\n",
    "//prep work\n",
    "gen f_time = end_date-transplant_date\n",
    "\n",
    "//stset (1)\n",
    "stset f_time, failure(died)\n",
    "\n",
    "//stset (2)\n",
    "stset end_date, origin(transplant_date) failure(died)\n",
    "\n",
    "//set a scale to measure time in years instead of days\n",
    "stset end_date, origin(transplant_date) failure(died) scale(365.25)\n",
    "\n",
    "//Kaplan-Meier curve\n",
    "sts graph\n",
    "\n",
    "//stratified K-M curve\n",
    "sts graph, by(don_ecd)\n",
    "\n",
    "//calculate incidence rate per person-year\n",
    "stsum\n",
    "stsum, by(don_ecd)\n",
    "\n",
    "//rank-sum tests\n",
    "sts test don_ecd\n",
    "sts test gender\n",
    "log close\n",
    "\n",
    "```\n",
    "\n",
    "In the code above, we cover examples related to various math functions, string functions, date and time functions, and survival analysis functions in Stata. Each section is clearly commented to explain the purpose and usage of the specific functions."
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.11.2"
  },
  "widgets": {
   "application/vnd.jupyter.widget-state+json": {
    "state": {},
    "version_major": 2,
    "version_minor": 0
   }
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}